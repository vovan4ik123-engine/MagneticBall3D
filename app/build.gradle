apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services' // Add the Google services Gradle plugin.

android {
    namespace 'com.magneticBall3D.app'
    compileSdk 34
    ndkVersion "26.3.11579264"

    defaultConfig {
        applicationId "com.magneticBall3D.app"

        minSdk 29 // Android 10 (2019 year).
        targetSdk 34
        versionCode 8
        versionName "2.1.0"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static" // , "-DCMAKE_BUILD_TYPE=Release"
                // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
                .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }

    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path 'jni/CMakeLists.txt'
            }
        }
    }

    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Import the Firebase BoM.
    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.google.firebase:firebase-analytics'

    // Billing system.
    implementation 'com.android.billingclient:billing:7.0.0'

    // Ads.
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
}
